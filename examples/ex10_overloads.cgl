/* Example #10 - Overloads
 * This example showcases overloads for default methods on structs, as well
 * as the "this" pointer in struct scopes. This includes operators (prefixed with $operator),
 * and $toString. $destruct can be found in the dynamic array example.
 */

struct Vector2D {
  float x;
  float y;

  fn $operator+(Vector2D* other) -> Vector2D {
    return Vector2D(this->x + other->x, this->y + other->y);
  }

  fn $operator-(Vector2D* other) -> Vector2D {
    return Vector2D(this->x - other->x, this->y - other->y);
  }

  fn $operator*(float scalar) -> Vector2D {
    return Vector2D(this->x * scalar, this->y * scalar);
  }

  fn $operator/(float scalar) -> Vector2D {
    return Vector2D(this->x / scalar, this->y / scalar);
  }

  fn $operator==(Vector2D* other) -> bool {
    return this->x == other->x && this->y == other->y;
  }

  fn $toString() -> string {
    return "Vector2D(" + this->x + ", " + this->y + ")";
  }
}

fn main() -> int {
  Vector2D v1 = Vector2D(1.0, 2.0);
  Vector2D v2 = Vector2D(3.0, 4.0);

  // using overloaded operators
  Vector2D sum = v1 + v2;
  // called implicitly, since a string is expected
  println("Sum: " + sum);

  Vector2D diff = v1 - v2;
  // can be called explicitly, but not necessary
  println("Difference: " + diff.$toString());

  float scalar = 2.0;
  Vector2D scaled = v1 * scalar;
  println("Scaled: " + scaled.$toString());

  return 0;
}

/* expected output:
 * Sum: Vector2D(4.0, 6.0)
 * Difference: Vector2D(-2.0, -2.0)
 * Scaled: Vector2D(2.0, 4.0)
 */
