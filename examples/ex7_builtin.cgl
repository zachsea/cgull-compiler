/* Example #7 - Built-in
 * This example showcases the built-in I/O functions in cgull, as well as
 * dynamic memory allocation, and strings/string mainipulation.
 */

struct Song {
  string title;
  int duration;
}

struct Playlist {
  string name;
  Song[] songs;
  int count;
}

fn formatTime(int seconds) -> string {
  int minutes = seconds / 60;
  seconds = seconds % 60;
  if (seconds < 10) {
    return (minutes as string) + ":0" + (seconds as string);
  }
  return (minutes as string) + ":" + (seconds as string);
}

fn main() {
  const string SEPARATOR = "------------------------";
  println("=== Playlist Creator ===");

  print("Enter playlist title: ");
  string playlistTitle = readline();

  print("How many songs would you like to add? ");
  string input = readline();
  int songCount = input as int;

  Playlist playlist = Playlist(playlistTitle, allocate Song[songCount], songCount);

  println("Allocated memory for " + songCount + " songs");

  // Add songs to the playlist
  int totalDuration = 0;

  for (int i = 0; i < songCount; i++) {
    string title = "";
    int duration = 0;
    println("\nSong #" + (i + 1));
    print("Title: ");
    title = readline();

    print("Duration (in seconds): ");
    input = read(); // Read until whitespace
    duration = input as int;

    playlist.songs[i] = Song(title, duration);

    totalDuration = totalDuration + playlist.songs[i].duration;
  }

  // Display the playlist
  println("\n=== Playlist: " + playlist.name + " ===");
  println(SEPARATOR);

  for (int i = 0; i < playlist.count; i++) {
    Song song = playlist.songs[i];
    println((i + 1) + ". " + song.title + " (" + formatTime(song.duration) + ")");
  }

  println(SEPARATOR);
  println("Total songs: " + playlist.count);
  println("Total duration: " + formatTime(totalDuration));
}

/* expected output
 * === Playlist Creator ===
 * Enter playlist title: My Favorite Songs
 * How many songs would you like to add? 3
 * Allocated memory for 3 songs
 * (input for each song)
 * Song #1
 * Title: Song A
 * Duration (in seconds): 240
 *
 * Song #2
 * Title: Song B
 * Duration (in seconds): 180
 *
 * Song #3
 * Title: Song C
 * Duration (in seconds): 300
 *
 * === Playlist: My Favorite Songs ===
 * ------------------------
 * 1. Song A (4:00)
 * 2. Song B (3:00)
 * 3. Song C (5:00)
 * ------------------------
 * Total songs: 3
 * Total duration: 12:00
 */
